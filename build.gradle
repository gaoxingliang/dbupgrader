buildscript {
    repositories {
        println "aliyun repositories  from init.gradle"
        maven { url 'https://maven.aliyun.com/repository/google' }
        maven { url 'https://maven.aliyun.com/repository/central' }
        maven { url 'https://maven.aliyun.com/repository/public' }
        mavenCentral()
    }

    allprojects {
        println "aliyun allprojects ${project.name}  from init.gradle"
        repositories {
            maven { url 'https://maven.aliyun.com/repository/google' }
            maven { url 'https://maven.aliyun.com/repository/central' }
            maven { url 'https://maven.aliyun.com/repository/public' }
        }
    }
}

plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'org.jreleaser' version '1.13.1'
}

compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

apply plugin: 'maven-publish'

group 'io.gitee.codeed:dbupgrader'
version '0.2.1'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    maven { url 'https://maven.aliyun.com/repository/public/' }
    mavenLocal()
    mavenCentral()
}

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    implementation group: 'com.google.guava', name: 'guava', version: '33.4.0-jre'
    implementation group: 'commons-io', name: 'commons-io', version: '2.18.0'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.17.0'
    implementation 'com.github.jsqlparser:jsqlparser:4.9'
    implementation 'org.projectlombok:lombok:1.18.36'
    testImplementation 'org.projectlombok:lombok:1.18.36'

    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'

    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    testImplementation 'mysql:mysql-connector-java:8.0.33'
}

test {
    useJUnitPlatform()
}


publishing {
    publications {
        maven(MavenPublication) {
            groupId =  "io.gitee.codeed"
            artifactId = 'dbupgrader'

            from components.java

            pom {
                name = 'dbupgrader'
                description = 'A lightweight database version management tool'
                url = 'https://gitee.com/codeed/dbupgrader'
                inceptionYear = '2024'
                licenses {
                    license {
                        name = 'Apache-2.0'
                        url = 'https://spdx.org/licenses/Apache-2.0.html'
                    }
                }
                developers {
                    developer {
                        id = 'codeed'
                        name = 'codeed'
                    }
                }
                scm {
                    connection = 'scm:git:https://gitee.com/codeed/dbupgrader.git'
                    developerConnection = 'scm:git:ssh://gitee.com/codeed/dbupgrader.git'
                    url = 'https://gitee.com/codeed/dbupgrader'
                }
            }
        }

        sc(MavenPublication) {
            groupId = "cn.sichuancredit"
            artifactId = 'dbmigration'
            version = project.version
            from components.java
        }
    }

    repositories {
        maven {
            name = 'maven'
            url = layout.projectDirectory.dir('build/staging-deploy')
        }

        maven {
            name = 'sc'
            url = project.findProperty('SICHUANCREDIT_URL')
            credentials {
                username = project.findProperty('SICHUANCREDIT_USERNAME')
                password = project.findProperty('SICHUANCREDIT_PASSWORD')
            }
        }
    }
}

// Configure publish task to only handle maven publication
tasks.named('publish') {
    dependsOn tasks.withType(PublishToMavenRepository).matching { task ->
        task.publication.name == 'maven' && task.repository.name == 'maven'
    }
}

task publishToCentral {
    dependsOn tasks.withType(PublishToMavenRepository).matching { task ->
        task.publication.name == 'maven' && task.repository.name == 'maven'
    }
}

// Custom task for SC publication
task publishToSc {
    dependsOn tasks.withType(PublishToMavenRepository).matching { task ->
        task.publication.name == 'sc' && task.repository.name == 'sc'
    }
}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
        mode = 'FILE'
        publicKey = '/Users/edward/.jreleaser/public.pgp'
        secretKey = '/Users/edward/.jreleaser/private.pgp'
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}